/*
 * MIT Licence
 * Copyright (c) 2023 Simon Frankenberger
 *
 * Please see LICENCE.md for complete licence text.
 */
plugins {
    id 'java'
    id 'com.github.ben-manes.versions' version '0.52.0'
}

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    testImplementation 'org.slf4j:slf4j-simple:2.0.16'
    testImplementation 'io.github.bonigarcia:webdrivermanager:5.9.3'
    testImplementation 'org.seleniumhq.selenium:selenium-java:4.29.0'
    testImplementation 'com.evanlennick:retry4j:0.15.0'
    testImplementation 'org.hamcrest:hamcrest:3.0'
    testImplementation platform("org.junit:junit-bom:5.12.0")
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
}

// compile sources with UTF-8 encoding
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

test {
    // use junit 5
    useJUnitPlatform()
    // print status of tests
    testLogging { events 'passed', 'skipped', 'failed' }
    if (project.hasProperty('webdriver.firefox.bin')) {
        systemProperty 'webdriver.firefox.bin', project.getProperty('webdriver.firefox.bin')
    }
}

tasks.named('dependencyUpdates').configure {
    gradleReleaseChannel = 'current'
    revision = 'integration'
}
